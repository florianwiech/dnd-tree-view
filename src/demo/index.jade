doctype html
html
  head
    link(rel='stylesheet',href='style.css')
    script(src='../../lib/TreeView.js')
  body
    main
    nav
      button(data-type="item").CreateItem Create item
      button(data-type="group").CreateGroup Create group
      button.RemoveSelected Remove selected
      .SelectedNodes No items selected
    script
      :coffee
        treeView = new TreeView document.querySelector 'main'

        createItem = (label) ->
          itemElt = document.createElement 'li'

          iconElt = document.createElement 'div'
          iconElt.classList.add 'icon'
          itemElt.appendChild iconElt

          spanElt = document.createElement 'span'
          spanElt.textContent = label
          itemElt.appendChild spanElt
          
          itemElt

        createGroup = (label) ->
          groupElt = document.createElement 'li'

          spanElt = document.createElement 'span'
          spanElt.textContent = label
          groupElt.appendChild spanElt

          groupElt

        for i in [0...3]
          group = createGroup "Group #{i+1}"
          treeView.append group, 'group'

          for j in [0...3]
            item = createItem "Item #{i*3+j+1}"
            treeView.append item, 'item', group

        onClickCreate = (event) ->
          type = event.target.dataset.type
          label = prompt "Enter a name", ""
          return if label.length == 0
          node = if type == 'item' then createItem(label) else createGroup(label)

          parentNode = treeView.selectedNodes[0]
          if parentNode? and ! parentNode.classList.contains 'group'
            parentNode = if parentNode.parentElement.classList.contains('children') then parentNode.parentElement.previousSibling else null

          treeView.append node, type, parentNode
          return

        document.querySelector('nav .CreateItem').addEventListener 'click', onClickCreate
        document.querySelector('nav .CreateGroup').addEventListener 'click', onClickCreate

        document.querySelector('nav .RemoveSelected').addEventListener 'click', ->
          while treeView.selectedNodes.length > 0
            treeView.remove treeView.selectedNodes[treeView.selectedNodes.length - 1]
          return

        treeView.on 'selectionChange', ->
          document.querySelector('nav .SelectedNodes').textContent =
            if treeView.selectedNodes.length > 1
              "#{treeView.selectedNodes.length} items selected"
            else if treeView.selectedNodes.length == 1
              "1 item selected"
            else
              "No items selected"
          return

        treeView.on 'activate', ->
          alert 'Activated "' + treeView.selectedNodes[0].querySelector('span').textContent + '"'